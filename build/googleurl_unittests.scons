# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration for building the googleurl_unittests{,.exe} executable.
"""

Import('env')

env = env.Clone()

env.SConscript([
    '$BASE_DIR/using_base.scons',
    '$ICU38_DIR/using_icu38.scons',
    '$GTEST_DIR/../using_gtest.scons',
    '$CHROME_SRC_DIR/build/using_googleurl.scons',
], {'env':env})

env.Prepend(
    CPPPATH = [
        '$CHROME_SRC_DIR',
    ],
)

if env.Bit('windows'):
  env.Append(
      CCFLAGS = [
          '/TP',
      ],
  )

input_files = [
    'src/gurl_unittest.cc',
    'src/url_canon_unittest.cc',
    'src/url_parse_unittest.cc',
    'src/url_util_unittest.cc',
    '$OBJ_ROOT/base/run_all_unittests$OBJSUFFIX',
]

gurl_unittests = env.ChromeTestProgram('googleurl_unittests', input_files)

installed_gurl_unittests = env.Install('$TARGET_ROOT', gurl_unittests)

env.Alias('googleurl', installed_gurl_unittests)
