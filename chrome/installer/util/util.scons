# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env = env.Clone()

env.ApplySConscript([
    '$BREAKPAD_DIR/using_breakpad.scons',
    '$CHROME_DIR/third_party/wtl/using_wtl.scons',
    '$ICU38_DIR/using_icu38.scons',
    '$LIBJPEG_DIR/using_libjpeg.scons',
    '$LIBPNG_DIR/using_libpng.scons',
    '$LIBXML_DIR/using_libxml.scons',
    '$LZMA_SDK_DIR/using_lzma_sdk.scons',
    '$NPAPI_DIR/using_npapi.scons',
    '$SKIA_DIR/using_skia.scons',
    '$ZLIB_DIR/using_zlib.scons',
])

env.Prepend(
    CPPPATH = [
        '$CHROME_SRC_DIR',
        '.',
    ],
)

input_files = [
    'browser_distribution.cc',
    'copy_tree_work_item.cc',
    'create_dir_work_item.cc',
    'create_reg_key_work_item.cc',
    'delete_reg_value_work_item.cc',
    'delete_tree_work_item.cc',
    'google_chrome_distribution.cc',
    'google_update_constants.cc',
    'google_update_settings.cc',
    'helper.cc',
    'install_util.cc',
    'l10n_string_util.cc',
    'logging_installer.cc',
    'lzma_util.cc',
    'master_preferences.cc',
    'set_reg_value_work_item.cc',
    'shell_util.cc',
    'util_constants.cc',
    'version.cc',
    'work_item.cc',
    'work_item_list.cc',
]

# TODO(port):
if env.Bit('windows'):
  env.ChromeStaticLibrary('util', input_files)

env.ChromeMSVSProject('$CHROME_DIR/installer/util/util.vcproj',
                      dependencies = [
                          ('$CHROME_DIR/installer/util/prebuild/' + 
                               'util_prebuild.vcproj'),
                      ],
                      guid='{EFBB1436-A63F-4CD8-9E99-B89226E782EC}')


# TODO(port):
if env.Bit('windows'):
  # create_string_rc.py imports FP.py from the tools/grit/grit/extern
  # directory, so add that to PYTHONPATH for this command execution.
  env_x = env.Clone()
  env_x.AppendENVPath('PYTHONPATH',
      [env_x.Dir('$CHROME_SRC_DIR/tools/grit/grit/extern').abspath])
  env_x.Command(['$CHROME_DIR/installer/util/installer_util_strings.rc',
                 '$CHROME_DIR/installer/util/installer_util_strings.h'],
                ['$CHROME_DIR/installer/util/prebuild/create_string_rc.py',
                 '$CHROME_DIR/app/generated_resources.grd'] +
                 env.Glob('$CHROME_DIR/app/resources/*.xtb'),
                "$PYTHON ${SOURCES[0]} ${TARGET.dir}")
