# Copyright (c) 2009 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import os

Import('env')

env = env.Clone()

grd_files = [
    '$CHROME_SRC_DIR/chrome/app/generated_resources.grd',
    '$CHROME_SRC_DIR/chrome/app/chromium_strings.grd',
    '$CHROME_SRC_DIR/chrome/app/google_chrome_strings.grd',
    '$CHROME_SRC_DIR/chrome/app/resources/locale_settings.grd',
]

for grd_file in grd_files:
  # The fake target gets replaced with real targets when the GRIT Builder
  # runs.
  generated = env.GRIT(
      '$TARGET_ROOT/grit_derived_sources/fake_' + os.path.basename(grd_file),
      grd_file)

languages = [ 'ar', 'bg', 'bn', 'ca', 'cs', 'da', 'de', 'el', 'en-GB',
    'en-US', 'es', 'es-419', 'et', 'fi', 'fil', 'fr', 'gu', 'he', 'hi', 'hr',
    'hu', 'id', 'it', 'ja', 'kn', 'ko', 'lt', 'lv', 'ml', 'mr', 'nl', 'nb',
    'or', 'pl', 'pt-BR', 'pt-PT', 'ro', 'ru', 'sk', 'sl', 'sr', 'sv', 'ta',
    'te', 'th', 'tr', 'uk', 'vi', 'zh-CN', 'zh-TW' ]

# TODO(tc): Handle official build vs chromium build.  Currently we assume
# chromium builds only.
for lang in languages:
  lang_pak = env.Repack('$TARGET_ROOT/locales/%s.pak' % lang,
      ['$TARGET_ROOT/grit_derived_sources/chromium_strings_%s.pak' % lang,
       '$TARGET_ROOT/grit_derived_sources/generated_resources_%s.pak' % lang,
       '$TARGET_ROOT/grit_derived_sources/locale_settings_%s.pak' % lang,
       '$TARGET_ROOT/grit_derived_sources/webkit_strings_%s.pak' % lang,
      ]
  )
  i = env.Install('$DESTINATION_ROOT/locales', lang_pak)

# Only require that en-US.pak is build when building chrome.
env.Requires('$DESTINATION_ROOT/chrome', '$DESTINATION_ROOT/locales/en-US.pak')
env.Requires('$DESTINATION_ROOT/unit_tests',
             '$DESTINATION_ROOT/locales/en-US.pak')
