# Copyright (c) 2006-2008 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

__doc__ = """
Configuration for building the net_perftests{,.exe} executable.
"""

Import('env')

env = env.Clone()

env.ApplySConscript([
    '$BASE_DIR/using_base.scons',
    '$CHROME_SRC_DIR/build/using_googleurl.scons',
    '$GTEST_DIR/../using_gtest.scons',
    '$ICU38_DIR/using_icu38.scons',
    '$MODP_B64_DIR/using_modp_b64.scons',
    '$NET_DIR/using_net.scons',
])

if env.Bit('posix'):
  env.SConscript([
      '$LIBEVENT_DIR/using_libevent.scons',
  ], {'env':env})

if env.Bit('windows'):
  env.Prepend(
      CCFLAGS = [
          '/WX',
      ],
  )
elif env.Bit('linux'):
  env.Append(
      # TODO(port): disk_cache_perftest breaks strict aliasing rules.
      CCFLAGS = ['-fno-strict-aliasing'],
  )

input_files = [
    'base/cookie_monster_perftest.cc',
    'disk_cache/disk_cache_perftest.cc',
    'disk_cache/disk_cache_test_util$OBJSUFFIX',

    # TODO(sgk): avoid using .cc from base directly
    '$OBJ_ROOT/base/run_all_perftests$OBJSUFFIX',
    '$OBJ_ROOT/base/perftimer$OBJSUFFIX',
]

if not env.Bit('mac'):
  env.ChromeTestProgram('net_perftests', input_files)
